STACK SEGMENT
	DW 200 DUP(?)
STACK ENDS

DATA SEGMENT
	OPENSTR DB 'NOW PLAYING: '
	TITLESTR DB 'Katyusha$'
	;音名(G大调)
	c0 EQU 196
	d0 EQU 220
	e0 EQU 247
	f0 EQU 277
	g0 EQU 294
	a0 EQU 330
	b0 EQU 370
	c1 EQU 392 ;1 
	d1 EQU 440 ;2
	e1 EQU 494 ;3
	f1 EQU 523 ;4
	g1 EQU 587 ;5
	a1 EQU 659 ;6
	b1 EQU 740 ;7
	c2 EQU 784
	d2 EQU 880
	e2 EQU 988
	f2 EQU 1047
	g2 EQU 1175
	a2 EQU 1319
	b2 EQU 1480
	s1 EQU 1  ;休止符
	;频率(每节换一行)
	FREQ DW a0, b0
		 DW c1, a0
		 DW c1, b0, a0
		 DW b0, e0, s1
		 DW b0, c1
		 DW d1, b0

		 DW d1, d1, c1, b0
		 DW a0
		 DW e1, a1
		 DW g1, a1, g1
		 DW f1, e1, d1

		 DW e1, a0
		 DW s1, f1, d1
		 DW e1, c1
		 DW b0, e0, c1, b0
		 DW a0

		 DW e1, a1
		 DW g1, a1, g1
		 DW f1, e1, d1

		 DW e1, a0
		 DW s1, f1, d1
		 DW e1, c1
		 DW b0, e0, c1, b0
		 DW a0

		 DW 0
	;节拍(设4分音符1拍120ms)
	QUATERBEAT EQU 12 ;四分音符
	OCTABEAT EQU QUATERBEAT/2  ;八分音符
	DICHOTOMOUSBEAT EQU QUATERBEAT*2  ;二分音符
	TIME DW QUATERBEAT+ OCTABEAT, OCTABEAT
		 DW QUATERBEAT + OCTABEAT, OCTABEAT
		 DW QUATERBEAT, OCTABEAT, OCTABEAT
		 DW QUATERBEAT, OCTABEAT, OCTABEAT
		 DW QUATERBEAT + OCTABEAT, OCTABEAT
		 DW QUATERBEAT + OCTABEAT, OCTABEAT
		 
		 DW OCTABEAT, OCTABEAT, OCTABEAT, OCTABEAT
		 DW QUATERBEAT * 2
		 DW	QUATERBEAT, QUATERBEAT
		 DW	QUATERBEAT, OCTABEAT, OCTABEAT
		 DW QUATERBEAT, OCTABEAT, OCTABEAT

		 DW QUATERBEAT, QUATERBEAT
		 DW OCTABEAT, QUATERBEAT, OCTABEAT
		 DW QUATERBEAT + OCTABEAT, OCTABEAT
		 DW OCTABEAT, OCTABEAT, OCTABEAT, OCTABEAT
		 DW QUATERBEAT * 2

		 DW	QUATERBEAT, QUATERBEAT
		 DW	QUATERBEAT, OCTABEAT, OCTABEAT
		 DW QUATERBEAT, OCTABEAT, OCTABEAT

		 DW QUATERBEAT, QUATERBEAT
		 DW OCTABEAT, QUATERBEAT, OCTABEAT
		 DW QUATERBEAT + OCTABEAT, OCTABEAT
		 DW OCTABEAT, OCTABEAT, OCTABEAT, OCTABEAT
		 DW QUATERBEAT * 2
DATA ENDS

CODE SEGMENT
	ASSUME CS:CODE, SS:STACK, DS:DATA
SING PROC 	;从数据段取出每个音符的频率数据和节拍时间数据
				;供SOUND子程序发声
	PUSH DI
	PUSH SI
	PUSH BP
	PUSH BX
REPT:
	MOV DI, DS:[SI]
	CMP DI, 0
	JZ  ENDSING    ;是否读到结束标志
	MOV BX, DS:[BP]
	CALL SOUND
	ADD SI, 2
	ADD BP, 2
	JMP REPT
ENDSING:
	POP BX
	POP BP
	POP SI
	POP DI
	RET
SING ENDP

SOUND PROC
	PUSH AX
	PUSH BX	;节拍时间
	PUSH CX
	PUSH DX
	PUSH DI	;入口参数DI给定频率数据
	CMP DI, 1 ;是否为休止符
	JZ STDEL
	MOV AL, 0B6H	;8253初始化(通道2，方波信号)
	OUT 43H, AL
	MOV DX, 12H
	MOV AX, 34DCH	;计算时间常数
	DIV DI
	OUT 42H, AL		;设置时间常数
	MOV AL, AH
	OUT 42H, AL
	IN  AL, 61H
	MOV AH, AL
	OR  AL, 3		;开喇叭，8255I/0端口61H的低两位置
	OUT 61H, AL
DL10ms:
	CALL DELAY
	DEC BX
	JNZ DL10ms
	MOV AL, AH
	OUT 61H, AL		;关喇叭
	POP DI
	POP DX
	POP CX
	POP BX
	POP AX
	RET
STDEL:
	CALL DELAY
	DEC BX
	JNZ STDEL
	POP DI
	POP DX
	POP CX
	POP BX
	POP AX
	RET
SOUND ENDP

DELAY PROC
	PUSH AX
	PUSH BX
	PUSH CX
	PUSH DX
	MOV AH, 2CH
	INT 21H
	MOV BX, DX ;	现在的时间:秒.百分秒
	MOV AH, 2CH
	CMP BH, 00H
	JNZ CHECK
CHECK: ;取当前时间进行判断
	INT 21H
	CMP DH, 0
	JNZ NOEXP
ZJUDGE: ;现在时间为0秒不需要+60S判断
	CMP BH, 0
	JZ NOEXP	
	ADD DH, 60H	;59秒到0秒时的判断
NOEXP:
	SUB DX, BX   ; 时差
	CMP DX, 0001H ;精度为1百分秒
	JBE CHECK
	POP DX
	POP CX
	POP BX
	POP AX
	RET
DELAY ENDP

START:
	MOV AX, DATA
	MOV DS, AX
	MOV AX, STACK
	MOV SS, AX
	MOV AH, 09H
	LEA DX, OPENSTR
	INT 21H
	LEA SI, FREQ
	LEA BP, TIME
	CALL SING
	MOV AH, 4CH
	INT 21H
CODE ENDS
	END START
